// Copyright (c) 2017-2020 Xiamen Yaji Software Co., Ltd.
CCEffect %{
  techniques:
  - passes:
    - vert: directional-blur-vs:vert
      frag: directional-blur-fs:frag
      depthStencilState:
        depthTest: false
        depthWrite: false
      blendState:
        targets:
        - blend: true
          blendSrc: src_alpha
          blendDst: one_minus_src_alpha
          blendDstAlpha: one_minus_src_alpha
      rasterizerState:
        cullMode: none
      properties:
        alphaThreshold: { value: 0.5 }        
        directionX: { value: 0.1, target: direction.x, editor: { slide: true, range: [0.0, 1.0], step: 0.001 } }
        directionY: { value: 0.1, target: direction.y, editor: { slide: true, range: [0.0, 1.0], step: 0.001 } }        
}%

CCProgram directional-blur-ubo %{
  uniform BlurConst{
    vec2 direction;
  };
}%

CCProgram directional-blur-vs %{
  precision highp float;
  #include <builtin/uniforms/cc-global>
  #if USE_LOCAL
    #include <builtin/uniforms/cc-local>
  #endif
  #if SAMPLE_FROM_RT
    #include <common/common-define>
  #endif
  in vec3 a_position;
  in vec2 a_texCoord;
  in vec4 a_color;

  out vec4 color;
  out vec2 uv0;

  vec4 vert () {
    vec4 pos = vec4(a_position, 1);

    #if USE_LOCAL
      pos = cc_matWorld * pos;
    #endif

    #if USE_PIXEL_ALIGNMENT
      pos = cc_matView * pos;
      pos.xyz = floor(pos.xyz);
      pos = cc_matProj * pos;
    #else
      pos = cc_matViewProj * pos;
    #endif

    uv0 = a_texCoord;
    #if SAMPLE_FROM_RT
      CC_HANDLE_RT_SAMPLE_FLIP(uv0);
    #endif
    color = a_color;
    
    return pos;
  }
}%

CCProgram directional-blur %{

  const float iterateCount = 30.0;
  vec4 directionalBlur(sampler2D mainTexture, vec2 uv, vec2 direction){
    vec4 color = vec4(0.0, 0.0, 0.0, 0.0);  
    for (int k = -int(iterateCount); k < int(iterateCount); k++)
    {
        color += texture(mainTexture, uv - direction * float(k) );
    }
    vec4 finalColor = color / (iterateCount * 2.0);

    return finalColor;
  }

}%

CCProgram directional-blur-fs %{
  precision highp float;
  #include <builtin/internal/embedded-alpha>
  #include <builtin/internal/alpha-test>

  in vec4 color;

  #include <directional-blur-ubo>
  #include <directional-blur>

  #if USE_TEXTURE
    in vec2 uv0;
    #pragma builtin(local)
    layout(set = 2, binding = 11) uniform sampler2D cc_spriteTexture;
  #endif

  vec4 frag () {
    vec4 o = vec4(1, 1, 1, 1);

    #if USE_TEXTURE      
      #if USE_DIRECTIONAL_BLUR
        o *= directionalBlur(cc_spriteTexture, uv0, direction.xy);
      #else
        o *= CCSampleWithAlphaSeparated(cc_spriteTexture, uv0);
      #endif //USE_DIRECTIONAL_BLUR
      
      #if IS_GRAY
        float gray  = 0.2126 * o.r + 0.7152 * o.g + 0.0722 * o.b;
        o.r = o.g = o.b = gray;
      #endif
    #endif

    o *= color;
    ALPHA_TEST(o);
    return o;
  }
}%
