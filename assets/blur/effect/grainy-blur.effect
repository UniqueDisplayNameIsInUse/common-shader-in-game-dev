// Copyright (c) 2017-2020 Xiamen Yaji Software Co., Ltd.
CCEffect %{
  techniques:
  - passes:
    - vert: grainy-blur-vs:vert
      frag: grainy-blur-fs:frag
      depthStencilState:
        depthTest: false
        depthWrite: false
      blendState:
        targets:
        - blend: true
          blendSrc: src_alpha
          blendDst: one_minus_src_alpha
          blendDstAlpha: one_minus_src_alpha
      rasterizerState:
        cullMode: none
      properties:
        alphaThreshold: { value: 0.5 }   
        blurRadius: { value: 0.01, editor: { slide: true, range: [0.0, 0.3], step: 0.001 } }
}%

CCProgram grainy-blur-vs %{
  precision highp float;
  #include <builtin/uniforms/cc-global>
  #if USE_LOCAL
    #include <builtin/uniforms/cc-local>
  #endif
  #if SAMPLE_FROM_RT
    #include <common/common-define>
  #endif
  in vec3 a_position;
  in vec2 a_texCoord;
  in vec4 a_color;

  out vec4 color;
  out vec2 uv0;  

  vec4 vert () {
    vec4 pos = vec4(a_position, 1);

    #if USE_LOCAL
      pos = cc_matWorld * pos;
    #endif

    #if USE_PIXEL_ALIGNMENT
      pos = cc_matView * pos;
      pos.xyz = floor(pos.xyz);
      pos = cc_matProj * pos;
    #else
      pos = cc_matViewProj * pos;
    #endif

    uv0 = a_texCoord;
    #if SAMPLE_FROM_RT
      CC_HANDLE_RT_SAMPLE_FLIP(uv0);
    #endif
    color = a_color;

    return pos;
  }
}%

CCProgram grainy-blur %{
  uniform BlurConst{
    float blurRadius;
  };

  // 随机噪声
  float rand(vec2 uv){
    return sin(dot(uv, vec2(1233.224, 1743.335)));
  }

  const highp float iterateCount = 5.0;
  vec4 grainyBlur(sampler2D mainTexture, vec2 uv, float blurRadius){
    vec2 randomOffset = vec2(0.0, 0.0);
    vec4 finalColor = vec4(0.0, 0.0, 0.0, 0.0);
    float random = rand(uv);

    for (int k = 0; k < int(iterateCount); k ++)
    {
      
      random = fract(43758.5453 * random + 0.61432);
      randomOffset.x = (random - 0.5) * 2.0;
      random = fract(43758.5453 * random + 0.61432);
      randomOffset.y = (random - 0.5) * 2.0;

      // 从 uv 位置随机选择纹理进行混合
      finalColor += texture(mainTexture, vec2(uv + randomOffset * blurRadius));
    }
    return finalColor / iterateCount;
  }
}%

CCProgram grainy-blur-fs %{
  precision highp float;
  #include <builtin/internal/embedded-alpha>
  #include <builtin/internal/alpha-test>

  in vec4 color;

  #if USE_TEXTURE
    in vec2 uv0;
    #pragma builtin(local)
    layout(set = 2, binding = 11) uniform sampler2D cc_spriteTexture;
  #endif

  #include <grainy-blur>

  vec4 frag () {
    vec4 o = vec4(1, 1, 1, 1);

    #if USE_TEXTURE
      #if USE_GRAINY_BLUR
        o *= grainyBlur(cc_spriteTexture, uv0, blurRadius);
      #else 
        o *= CCSampleWithAlphaSeparated(cc_spriteTexture, uv0);                         
      #endif
      #if IS_GRAY
        float gray  = 0.2126 * o.r + 0.7152 * o.g + 0.0722 * o.b;
        o.r = o.g = o.b = gray;
      #endif
    #endif

    o *= color;
    ALPHA_TEST(o);
    return o;
  }
}%
